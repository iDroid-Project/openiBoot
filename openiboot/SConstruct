#
# The root build file for OpeniBoot.
#

import SCons

#
# Configuration, change this stuff
#
version="0.2"
# /Configuration

SConscript([
	'scons/ARMEnvironment.SConscript',
	'scons/Git.SConscript',
	])
Import('*')

henv = Environment()
Export('henv')

#henv.Append(CPPFLAGS = ['-g']) # TODO: add a debug flag

env = ARMEnvironment()
env.Append(CPPDEFINES=[
	'OPENIBOOT_VERSION='+version,
	'OPENIBOOT_VERSION_BUILD='+GetGitCommit(),
	])
env.Append(CPPFLAGS = ['-Wall', '-Werror', '-O2', '-Ttext=0x0'])
Export('env')

def localize(env, ls):
	return [File(f) for f in ls]
env.AddMethod(localize, "Localize")

# Sources for menu
menu_src = env.Localize([
	'menu.c',
	])
Export('menu_src')

# Sources for installer image
installer_src = env.Localize([
	'installer.c',
	])
Export('installer_src')

# Base Target Sources
base_src = env.Localize([
	'device.c',
	'mtd.c',
	'bdev.c',
	'audiohw.c',
	'actions.c',
	'commands.c',
	'framebuffer.c',
	'images.c',
	'malloc.c',
	'nvram.c',
	'openiboot.c',
	'printf.c',
	'scripting.c',
	'sha1.c',
	'stb_image.c',
	'syscfg.c',
	'tasks.c',
	'util.c',
	'acm.c',
	])
Export('base_src')

# Build-Environment Modules
henv.SConscript([
	'images/SConscript',
	'mk8900image/SConscript',
	'scons/Module.SConscript',
	])

# Target Modules
env.SConscript([
	'hfs/SConscript',
	'radio-pmb8876/SConscript',
	'radio-pmb8878/SConscript',
	'usb-synopsys/SConscript',
	'nor-cfi/SConscript',
	'nor-spi/SConscript',
	'arch-arm/SConscript',
	])
